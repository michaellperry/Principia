namespace Principia.Model;

fact Individual {
key:
    string anonymousId;

query:
	Share *sharedCourses {
		Share s : s.individual = this
			where s.isCurrent
	}
}

fact Course {
key:
	unique;

mutable:
	string title;
	string shortDescription;
	string description;

query:
	Module* modules {
		Module m : m.course = this
	}
}

fact Share {
key:
	unique;
	Individual individual;
	Course course;

query:
	bool isCurrent {
		not exists ShareRevoke r : r.share = this
	}
}

fact ShareRevoke {
key:
	Share share;
}

fact Module {
key:
	unique;
	Course course;

mutable:
	int ordinal;
	string title;

query:
	Clip* clips {
		ClipModule cm : cm.module = this
			where cm.isCurrent
		Clip c : c = cm.clip
	}
}

fact Clip {
key:
	unique;
	Course course;

mutable:
	int ordinal;
	string title;
}

fact ClipModule {
key:
	Clip clip;
	Module module;
	ClipModule* prior;

query:
	bool isCurrent {
		not exists ClipModule next : next.prior = this
	}
}
